#!perl

package Perly::Bot::Bin;

use v5.22;
use feature qw(signatures);
no warnings qw(experimental::signatures);

use Perly::Bot 0.201;
use Getopt::Long qw(GetOptions);
use File::Spec::Functions qw(rel2abs);

__PACKAGE__->run(@ARGV) unless caller;

sub run ( $class, @args )
{
  my $opts = $class->process_args(@args);

  say <<"HERE";
Run summary
  config   $opts->{config}
  feeds    $opts->{feeds}
  log4perl $opts->{log4perl}
  level    $opts->{log_level}
  root     $opts->{root}
  dry_run  $opts->{dry_run}

PERLYBOT_LOG_LEVEL => $ENV{PERLYBOT_LOG_LEVEL}
PERLY_BOT_DEBUG    => $ENV{PERLY_BOT_DEBUG}
HERE

  exit if $opts->{dry_run};

  if( $opts->{root} ) {
  	$opts->{config} = rel2abs( $opts->{config}, $opts->{root} );
  	}
  Perly::Bot->run( $opts->{config} );

  exit 0;
}

sub process_args ( $class, @args )
{
  local @ARGV = @args;

  my %opts = ();

  GetOptions(
    "config|c=s"   => \$opts{config},
    "feeds|f=s"    => \$opts{feeds},
    "log4perl|l=s" => \$opts{log4perl},
    "level|L=s"    => \$opts{log_level},
    "debug|d=s"    => \$opts{debug},
    "dry_run|D"    => \$opts{dry_run},
    "root|r=s"     => \$opts{root},
  );

  $ENV{PERLYBOT_LOG_LEVEL} = $opts{log_level} // $ENV{PERLYBOT_LOG_LEVEL} // 'INFO';
  $ENV{PERLY_BOT_DEBUG}    = $opts{debug}     // $ENV{PERLY_BOT_DEBUG} // 0;

  return \%opts;
}

1;

__END__

=encoding utf8

=head1 NAME

perlybot - run the PerlyBot social media broadcaster

=head1 SYNOPSIS

  # run with defaults
  % perlybot

  # run with specific config, short or long switches
  % perlybot -c CONFIG_FILE
  % perlybot --config CONFIG_FILE

  # run with specific feed file
  % perlybot -f FEED_FILE
  % perlybot --feeds FEED_FILE

  # specify both
  % perlybot -c CONFIG_FILE -f FEED_FILE

  # change the log4perl config
  % perlybot -l LOG4PERL_FILE

=head1 DESCRIPTION

A command line interface to Perly::Bot.

=head2 Command-line switches

=over 4

=item -c, --config

Specify the config file path.  See L<Perly::Bot::Config>.

=item -d, --debug

Turn on debugging. If set, this overrides the level set in
C<PERLY_BOT_DEBUG>.

=item -f, --feeds

Specify the feeds file path. See L<Perly::Bot::Feed>.

=item -l, --log4perl

Specify the Log4perl configuration file.

=item -L, --log_level

Specify the Log4perl log level. IF specified, this overrides the
level set in C<PERLYBOT_LOG_LEVEL>.

=back

=head1 TO DO

=head1 SEE ALSO

=head1 SOURCE AVAILABILITY

This source is part of a GitHub project:

  https://github.com/dnmfarrell/Perly-Bot

=head1 AUTHOR

brian d foy, C<< <bdfoy@cpan.org> >>

=head1 COPYRIGHT AND LICENSE

Copyright Â© 2015, brian d foy <bdfoy@cpan.org>. All rights reserved.

This program is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

=cut
